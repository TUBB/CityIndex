apply plugin: 'com.android.application'
apply plugin: 'io.github.tubb.multichannel'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.tubb.cityindex"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 3
        versionName "4.0.0"
    }
    // 签名
    signingConfigs {
        myConfig {
            storeFile file("tubb.jks")
            storePassword "123456"
            keyAlias "tubb"
            keyPassword "123456"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        develop {
            dimension 'default'
            manifestPlaceholders = [ channel:"develop" ]
        }
    }
    lintOptions {
        abortOnError false
    }
}

appChannel {
    channelFlavorConfig {
        channelConfigFilePath "${project.getProjectDir()}/channels.txt"
        configProductFlavor { name ->
            if ('qq'.equals(name)) { // for test
                return {
                    dimension 'default'
                    manifestPlaceholders = [ channel:"${name}_yyb" ]
                }
            } else {
                return {
                    dimension 'default'
                    manifestPlaceholders = [ channel:name ]
                }
            }
        }
        // must call after configProductFlavor dsl
        createChannel()
    }
    bundleOutputConfig {
        outputDir "${project.getProjectDir()}/channels"
        renameBundleFile { project, variant ->
            // rename apk file
            project.name + '-' + variant.flavorName + '-' + variant.buildType.name + '-' + variant.versionName + '.apk'
        }
    }
    taskConfig {
        disableLintTask true
        disableDebugTask true
        disableTestTask true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
}




