apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.tubb.cityindex"
        minSdkVersion 10
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    // 签名
    signingConfigs {
        myConfig {
            storeFile file("tubb.jks")
            storePassword "123456"
            keyAlias "tubb"
            keyPassword "123456"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        develop {
            manifestPlaceholders = [ channel:"develop" ]
        }
    }
    lintOptions {
        abortOnError false
    }
}

def disableLintOrDebugBuild(){
    def targetTasks = project.tasks.findAll{task ->
        def taskName = task.name
        taskName.contains("Debug") || taskName.contains("lint") || taskName.contains("Lint") || taskName.contains("Test")
    }
    targetTasks.each{
        println"disable task    :   ${it.name}"
        it.setEnabled false
    }
}

if (project.hasProperty('channel')) {
    project.afterEvaluate{
        disableLintOrDebugBuild()
    }

    configure(android.productFlavors, {
        // 渠道号配置文件路径
        def path = "./build-types/channels.txt"
        file(path).eachLine { line ->
            if(!line.startsWith("//")){ //剔除注释行
                android.productFlavors.create(line, {
                    manifestPlaceholders = [ channel:line ]
                    signingConfig android.buildTypes.release.signingConfig
                })
            }
        }
    })
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.0'
}
